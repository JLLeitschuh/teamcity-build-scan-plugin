import java.text.SimpleDateFormat

plugins {
    id 'com.gradle.build-scan' version '2.1'
    id 'java'
    id 'groovy'
    id 'com.github.rodm.teamcity-server' version '1.1.1' // see https://github.com/rodm/gradle-teamcity-plugin
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.1'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

ext {
    CI = System.getenv().containsKey('CI') || project.properties.containsKey('CI')
    RELEASE = project.properties.containsKey('RELEASE')
    baseVersion = '0.7'
    buildTime = new Date()
}

def buildTimestamp = CI ? timestamp(buildTime) : 'prerelease'
def snapshotVersion = RELEASE ? '' : buildTimestamp

group = 'nu.studer'
version = baseVersion + (snapshotVersion ? "-$snapshotVersion" : '')

dependencies {
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.guava:guava:20.0'
    agent project(path: ':agent', configuration: 'plugin')

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
}

teamcity {
    version = '2018.1' // TeamCity API version

    server {
        descriptor {
            name = 'GradleBuildScanIntegration'
            displayName = 'Gradle Build Scan Integration'
            description = 'Provides easy navigation from TeamCity builds to Gradle build scans'
            version = project.version
            vendorName = 'Etienne Studer'
            vendorUrl = 'https://github.com/etiennestuder'

            // optional properties
            email = 'etienne@studer.nu'
            downloadUrl = 'https://github.com/etiennestuder/teamcity-build-scan-plugin'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = file('.teamcity/dists')
        baseHomeDir = file('.teamcity/servers')
        baseDataDir = file('.teamcity/data')

        teamcity2018_1 {
            version = "2018.1.5"
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
        teamcity2018_2 {
            version = "2018.2.1"
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
    }
}

task sources(type: Jar) {
    from sourceSets.main.allJava
}

publishing.publications {
    PluginPublication(MavenPublication) {
        artifact serverPlugin
        artifact sources {
            classifier 'sources'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['PluginPublication']

    dryRun = true
    publish = true
    override = false

    pkg {
        repo = 'teamcity-plugins'
        name = 'teamcity-build-scan-plugin'

        version {
            name = project.version
            released = project.buildTime
            vcsTag = project.version
        }
    }
}

tasks.build.dependsOn sources

static timestamp(Date date) {
    def timestampFormat = new SimpleDateFormat('yyyyMMddHHmmss')
    timestampFormat.timeZone = TimeZone.getTimeZone('UTC')
    timestampFormat.format(date)
}
